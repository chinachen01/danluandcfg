apply plugin: 'maven'

uploadArchives {
    repositories {
        mavenDeployer {
            snapshotRepository(url: "${mvnSnapUrl}") {
                authentication(userName: "${mvnName}", password: "${mvnPass}")
            }

            repository(url: "${mvnUrl}") {
                authentication(userName: "${mvnName}", password: "${mvnPass}")
            }

            pom.project {
                version getVersionName()
                artifactId "${artifactId}"
                groupId "${mvnGroupId}"
                packaging "${mvnPackaging}"
                description "${desc}"
            }
        }
    }
}

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

artifacts {
    archives file("${artifactId}.aar")
    archives sourcesJar
}

def getVersionName() {
    def versionFile = file('gradle.properties')
    def Properties versionProps = new Properties()
    versionProps.load(new InputStreamReader(new FileInputStream(versionFile), "utf-8"))

    def oldVersionName = versionProps['version'].toString()
    def isSnap = versionProps['isSnap'].toBoolean()

    def versions = oldVersionName.split("\\.")
    def one = versions[0].toInteger()
    def two = versions[1].toInteger()
    def three = versions[2].toInteger()

    if (three + 1 == 100) {
        two += 1
        three = 0
    } else {
        three += 1
    }

    if (two == 100) {
        one += 1
        two = 0
    }

    def newVersionName = one + "." + two + "." + three
    def runTasks = gradle.startParameter.taskNames
    if ('uploadArchives' in runTasks) {
        versionProps['version'] = newVersionName
        versionProps.store(versionFile.newWriter(), null)
    }


    if (isSnap) {
        newVersionName += "-SNAPSHOT"
    }
    return newVersionName
}